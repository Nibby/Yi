import yi.buildsrc.release.TargetPlatform
import yi.buildsrc.release.YiReleasePlugin

import java.nio.file.Files
import java.nio.file.Paths
import java.nio.file.Path

apply plugin: YiReleasePlugin
apply plugin: 'de.inetsoftware.setupbuilder'

def thisProject = rootProject.project(":app")
def packagingAssetDir = projectDir.toPath().resolve("packaging")

def applicationName = rootProject.name // TODO: Should just define a name for the editor project
def applicationIcons = [
    packagingAssetDir.resolve("AppIcon.icns").toAbsolutePath().toString(),
    packagingAssetDir.resolve("AppIcon.ico").toAbsolutePath().toString()
]

def jreDir = YiReleasePlugin.getCustomJreImageDirectoryAsPath(thisProject).toAbsolutePath().toString()
def artifactsDir = YiReleasePlugin.getReleaseArtifactsDirectoryAsPath(thisProject).toAbsolutePath().toString()

def versionString = thisProject.version.replace("-SNAPSHOT", "")

jlinkOptions.jdkHome = null // If null, uses JAVA_HOME env value instead
jlinkOptions.bundledJdkVersion = 11
jlinkOptions.mainModuleName = thisProject.ext.mainModuleName
jlinkOptions.mainJarArtifactName = "${thisProject.name}-${versionString}.jar".toString()
jlinkOptions.outputDir = jreDir

def executableVMArguments = [
    "--module-path", getPlatformTargetModulePath(),
    "--add-exports", "javafx.graphics/com.sun.glass.ui=${jlinkOptions.mainModuleName}".toString(),

    // This clause must appear after all --add-exports
    "--module", "codes.nibby.yi.app/codes.nibby.yi.app.Main",
]

if (TargetPlatform.getCurrentPlatform() == TargetPlatform.macOS) {
    def macOsJrePath = Paths.get(jreDir).resolve("Contents").resolve("Home")
    jreDir = macOsJrePath.toAbsolutePath().toString()
    jlinkOptions.outputDir = jreDir
}

jar {
    archiveBaseName = thisProject.name
    archiveVersion = thisProject.version
}

setupBuilder {
    vendor = 'Sizhe (Kevin) Yang'
    application = applicationName
    description = "Go game record editor"
    appIdentifier = "Yi"
    version = versionString
//    licenseFile = Paths.get(rootProject.projectDir.toString()).resolve('LICENSE').toFile()
    icons = applicationIcons
    // all files for all platforms
    from(artifactsDir) {
        include '*.jar'
    }
    mainJar = jlinkOptions.mainJarArtifactName
    mainClass = "codes.nibby.yi.app.Main"
    destinationDir = "artifacts"

    // Bundle a custom JRE image tailored for this application.
    // See buildSrc/YiReleasePlugin for details.
    bundleJre = jlinkOptions.outputDir
    failOnEmptyFrom = "true"

    desktopStarter {
        displayName = applicationName
        description = "Go game record editor"
        mimeTypes = "text/html;text"
        categories = "software;game"

        javaVMArguments = executableVMArguments

        documentType {
            fileExtension = [ "sgf" ]
            name = "Smart Game File"
            mimetype = "text/plain"
            role = "Editor"
        }
    }
}

private static String getPlatformTargetModulePath() {
    def os = System.getProperty("os.name").toLowerCase()
    if (os.contains("win")) {
        return "." // Doesn't really matter
    } else if (os.contains("mac")) {
        return "\$APP_ROOT/Contents/Java/"
    } else {
        // Assuming it's linux
        return "/usr/share/yi/"
    }
}

// These tasks are provided by YiReleasePlugin which expands the artifacts produced by
// 'assembleDist' to the project 'release' directory. This should contain all the jars
// used by the editor application.
def prerequisites = [prepareArtifactsForPackaging, createJreImage]

dmg.dependsOn(prerequisites)
deb.dependsOn(prerequisites)
msi.dependsOn(prerequisites)
rpm.dependsOn(prerequisites)

msi {
    arch = "x64"
    minOS = 6.1
    multiInstanceCount = 1
    installScope = "perMachine"
    runAfterIsOptional = true

    launch4j {
        executable = "Yi.exe"
        javaVMArguments = executableVMArguments
        requestedExecutionLevel = "asInvoker"
    }
}

dmg {
    windowWidth = 500
    windowHeight = 400
    iconSize = 128
}

deb {
    installationRoot = "/usr/share/Yi"
    section = "Games"
    maintainerEmail = "nibby@null.net"
    description = "Go game record editor and review tool"
}

rpm {
    installationRoot = "/usr/share/Yi"
    backwardCompatible = true
    section = "Games"
    summary = "Go game record editor and review tool"
    architecture = "x86_64"
}

task renameArtifacts() {
    doLast {
        String artifactName = "${applicationName}-${versionString}".toString()
        Path artifactDir = project.getBuildDir().toPath().resolve("artifacts")

        renameIfExists(artifactDir, artifactName, ".dmg")
        renameIfExists(artifactDir, artifactName, ".msi")
        renameIfExists(artifactDir, artifactName, ".deb")
        renameIfExists(artifactDir, artifactName, ".rpm")
    }
}

dmg.finalizedBy(renameArtifacts)
deb.finalizedBy(renameArtifacts)
msi.finalizedBy(renameArtifacts)
rpm.finalizedBy(renameArtifacts)

def renameIfExists(Path artifactDir, String artifactName, String extension) {
    if (isSnapshotBuild) {
        Path artifactPath = artifactDir.resolve(artifactName + extension)
        Path newArtifactPath = artifactDir.resolve(artifactName + "-SNAPSHOT" + extension)

        if (Files.exists(artifactPath) && !Files.isDirectory(artifactPath)) {
            logger.lifecycle("Renaming '${artifactPath.getFileName().toString()}' to " +
                    "'${newArtifactPath.getFileName().toString()}'")
            Files.move(artifactPath, newArtifactPath)
        }
    }

}