import java.nio.file.Paths

plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.9'
}

buildDir = '../build/' + getProject().getName()

dependencies {
    compile project(':core')
    compile project(':components:gameBoard')
    compile project(':components:gameTree')

    // JavaFx dependencies
    implementation rootProject.ext.javaFxDependencies.implementation

    testImplementation rootProject.ext.commonDependencies.testImplementation
    testRuntimeOnly rootProject.ext.commonDependencies.testRuntimeOnly
    // Integration test dependencies are beneath where testUi task is defined.
}

javafx {
    version = "11.0.2"
    modules = ['javafx.controls', 'javafx.media']
}

idea {
    module {
        testSourceDirs += file("$projectDir/src/testUi")
    }
}

sourceSets {
    main {
        java {
            srcDir('src/main/java')
        }

        resources {
            srcDir('src/main/resources')
        }
    }

    test {
        java {
            srcDirs = ['src/test']
        }
    }

    testUi {
        java {
            srcDirs = ['src/testUi']
        }
    }
}

application {
    def workingDirectory = Paths.get(System.getProperty("user.dir")).getParent().resolve("temp").toString()

    mainClassName('yi.editor.Main')
    applicationDefaultJvmArgs = ["-ea", "-Duser.dir=$workingDirectory"]
}

configurations {
    testUiImplementation.extendsFrom testImplementation
    testUiRuntimeOnly.extendsFrom testRuntimeOnly
}

test {
    useJUnitPlatform()
    testClassesDirs = sourceSets.test.output
}

task testUi(type: Test) {
    useJUnitPlatform()
    testClassesDirs = sourceSets.testUi.output.classesDirs
    classpath = sourceSets.testUi.runtimeClasspath
}

dependencies {
    testUiImplementation rootProject.ext.javaFxDependencies.testUiImplementation
}